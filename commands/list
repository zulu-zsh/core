#!/usr/bin/env zsh

###
# Print usage information
###
function _zulu_list_usage() {
  echo $(color yellow "Usage:")
  echo "  zulu list [options]"
  echo
  echo $(color yellow "Options:")
  echo "  -h, --help           Output this help text and exit"
  echo "  -a, --all            List all packages in the index"
  echo "  -i, --installed      List only installed packages (default)"
  echo "  -n, --not-installed  List non-installed packages"
  echo "  -d, --describe       Include package description in output"
  echo "  -s, --simple         Hide the 'package installed' indicator"
}

function _zulu_list_all() {
  local base index files json packages package name description pad palength


  base=${ZULU_DIR:-"${ZDOTDIR:-$HOME}/.zulu"}
  config=${ZULU_CONFIG_DIR:-"${ZDOTDIR:-$HOME}/.config/zulu"}
  index="$base/index/packages"

  for package in $(/bin/ls $index | grep -v '.md'); do
    if [[ -n $simple ]]; then
      name="$package"
      lim=30
    else
      if [[ -d "$base/packages/$package" ]]; then
        name="$(color green 'âœ”') $package"
        lim=36
      else
        name="  $package"
        lim=30
      fi
    fi

    if [[ -n $describe ]]; then
      json="$(cat $index/$package)"
      description=$(jsonval $json 'description')
      printf '%s' "$name"
      printf '%*.*s' 0 $(($lim - ${#name} )) "$(printf '%0.1s' " "{1..60})"
      printf '%s\n' "$description"
    else
      echo $name
    fi
  done

  return
}

function _zulu_list_installed() {
  local base index files json packages package description pad palength

  base=${ZULU_DIR:-"${ZDOTDIR:-$HOME}/.zulu"}
  config=${ZULU_CONFIG_DIR:-"${ZDOTDIR:-$HOME}/.config/zulu"}
  index="$base/index/packages"

  for package in $(/bin/ls "$base/packages"); do
    if [[ -n $describe ]]; then
      json="$(cat $index/$package)"
      description=$(jsonval $json 'description')

      printf '%s' "$package"
      printf '%*.*s' 0 $((30 - ${#package} )) "$(printf '%0.1s' " "{1..60})"
      printf '%s\n' "$description"
    else
      echo $package
    fi
  done

  return
}

function _zulu_list_not_installed() {
  local base index files json packages package description pad palength

  base=${ZULU_DIR:-"${ZDOTDIR:-$HOME}/.zulu"}
  config=${ZULU_CONFIG_DIR:-"${ZDOTDIR:-$HOME}/.config/zulu"}
  index="$base/index/packages"

  for package in $(/bin/ls $index | grep -v '.md'); do
    if [[ ! -d "$base/packages/$package" ]]; then
      if [[ -n $describe ]]; then
        json="$(cat $index/$package)"
        description=$(jsonval $json 'description')

        printf '%s' "$package"
        printf '%*.*s' 0 $((30 - ${#package} )) "$(printf '%0.1s' " "{1..60})"
        printf '%s\n' "$description"
      else
        echo $package
      fi
    fi
  done

  return
}

function _zulu_list() {
  local help all installed not_installed describe

  # Parse options
  zparseopts -D h=help \
                -help=help \
                i=installed \
                -installed=installed \
                n=not_installed \
                -not-installed=not_installed \
                a=all \
                -all=all \
                d=describe \
                -describe=describe \
                s=simple \
                -simple=simple

  # Output help and return if requested
  if [[ -n $help ]]; then
    _zulu_list_usage
    return
  fi

  if [[ -n $all ]]; then
    _zulu_list_all
    return
  fi

  if [[ -n $not_installed ]]; then
    _zulu_list_not_installed
    return
  fi

  _zulu_list_installed
}
